# STON.fi

import { Callout, Steps, Tabs } from 'nextra/components'

{/* See: https://nextra.site/docs/guide/built-ins */}

[STON.fi](https://ston.fi) is a decentralized automated market maker (AMM) built on [TON blockchain](https://ton.org) providing virtually zero fees, low slippage, an extremely easy interface, and direct integration with TON wallets.

<Callout type="warning">
  This api is about v2 of STON.fi. Make sure not to use it with v1 contracts
</Callout>

## Swap jetton to jetton

```tact
message(0x6664de2a) StonfiSwap {
    otherTokenWallet: Address; // Address of the other Router token wallet
    refundAddress: Address; // Address where refund will be sent if swap fails
    excessesAddress: Address; // Address where TON excesses will be sent
    deadline: Int as uint64; // Timestamp of execution deadline for this tx
    additionalData: SwapAdditionalData;
}

struct SwapAdditionalData {
    minOut: Int as coins; // Minimum required amount of tokens to receive
    receiver: Address; // Address where tokens will be sent after swap
    fwdGas: Int as coins = 0; // Gas used to forward a message in transfer_notification after swap if custom_payload is present
    customPayload: Cell? = null; // Payload sent in transfer_notification after swap
    refundFwdGas: Int as coins = 0; // Gas used to forward a message in transfer_notification if swap fails if refund_payload is present
    refundPayload: Cell? = null; // Payload sent in transfer_notification if swap fails
    refFee: Int as uint16 = 10; // Referral fee. Max amount is 100 (1%)
    referralAddress: Address? = null; // Referral address
}

message(0xf8a7ea5) JettonTransfer {
    queryId: Int as uint64;
    amount: Int as coins;
    destination: Address;
    responseDestination: Address?;
    customPayload: Cell? = null;
    forwardTonAmount: Int as coins;
    forwardPayload: Cell?; // hack for now, equivalent to Slice as remaining with first bit set to 1
}

// precalculated gas amounts to swap
const GasSwapJettonToJetton: Int = ton("0.3");
const GasSwapJettonToJettonFwd: Int = ton("0.24");

const RouterAddress: Address = address("kQALh-JBBIKK7gr0o4AVf9JZnEsFndqO0qTCyT-D-yBsWk0v"); // CPI Router v2.1.0
const RouterJettonWallet: Address = address("kQAtX3x2s-wMtYTz8CfmAyloHAB73vONzJM5S2idqXl-_5xK"); // Router Lupa Address. Should be calculated onchain in real scenarios

message SwapJetton {
    myJettonWalletAddress: Address; // calculated offchain for ease of example, in real case scenarios should be calculated onchain
}

contract JettonToJetton {
    receive() {}
    receive(msg: SwapJetton) {
        let myJettonWalletAddress: Address = msg.myJettonWalletAddress;

        let receiver = address("0QD-SuoCHsCL2pIZfE8IAKsjc0aDpDUQAoo-ALHl2mje04A-"); // replace with your reciver address
        let offerAmount: Int = 100000; // replace with your offer amount

        let forwardPayload = StonfiSwap{
            otherTokenWallet: RouterJettonWallet,
            refundAddress: myAddress(), // replace with your refund address
            excessesAddress: myAddress(), // replace with your exceesses address
            deadline: now() + 10000,
            additionalData: SwapAdditionalData{
                minOut: 1, // swap will fail if a receiver should receive less than minOut of tokens as a result
                receiver,
            }
        };

        send(SendParameters{
            to: myJettonWalletAddress,
            value: GasSwapJettonToJetton,
            body: JettonTransfer{
                queryId: 42,
                amount: offerAmount,
                destination: RouterAddress,
                responseDestination: myAddress(),
                forwardTonAmount: GasSwapJettonToJettonFwd,
                forwardPayload: forwardPayload.toCell(),
            }.toCell()
        });
    }
}
```

## Swap jetton to TON

Swapping jetton to TON is similar to jetton/jetton swap. The only one difference is that `RouterJettonWallet` address should be replaced with `RouterProxyTonWallet`.

```tact
message(0x6664de2a) StonfiSwap {
    otherTokenWallet: Address; // Address of the other Router token wallet
    refundAddress: Address; // Address where refund will be sent if swap fails
    excessesAddress: Address; // Address where TON excesses will be sent
    deadline: Int as uint64; // Timestamp of execution deadline for this tx
    additionalData: SwapAdditionalData;
}

struct SwapAdditionalData {
    minOut: Int as coins; // Minimum required amount of tokens to receive
    receiver: Address; // Address where tokens will be sent after swap
    fwdGas: Int as coins = 0; // Gas used to forward a message in transfer_notification after swap if custom_payload is present
    customPayload: Cell? = null; // Payload sent in transfer_notification after swap
    refundFwdGas: Int as coins = 0; // Gas used to forward a message in transfer_notification if swap fails if refund_payload is present
    refundPayload: Cell? = null; // Payload sent in transfer_notification if swap fails
    refFee: Int as uint16 = 10; // Referral fee. Max amount is 100 (1%)
    referralAddress: Address? = null; // Referral address
}

message(0xf8a7ea5) JettonTransfer {
    queryId: Int as uint64;
    amount: Int as coins;
    destination: Address;
    responseDestination: Address?;
    customPayload: Cell? = null;
    forwardTonAmount: Int as coins;
    forwardPayload: Cell?; // hack for now, equivalent to Slice as remaining with first bit set to 1
}

const GasSwapJettonToTon: Int = ton("0.3");
const GasSwapJettonToTonFwd: Int = ton("0.24");

const RouterAddress: Address = address("kQALh-JBBIKK7gr0o4AVf9JZnEsFndqO0qTCyT-D-yBsWk0v"); // CPI Router v2.1.0
const RouterProxyTonWallet: Address = address("kQBbJjnahBMGbMUJwhAXLn8BiigcGXMJhSC0l7DBhdYABhG7"); // Router pTON Address

message SwapJetton {
    myJettonWalletAddress: Address; // calculated offchain for ease of example, in real case scenarios should be calculated onchain
}

contract Sample {
    receive() {}
    receive(msg: SwapJetton) {
        let myJettonWalletAddress: Address = msg.myJettonWalletAddress;

        let receiver = address("0QD-SuoCHsCL2pIZfE8IAKsjc0aDpDUQAoo-ALHl2mje04A-"); // replace with your reciever address
        let offerAmount: Int = 100000;

        let forwardPayload = StonfiSwap{
            otherTokenWallet: RouterProxyTonWallet,
            refundAddress: myAddress(), // replace with your refund address
            excessesAddress: myAddress(), // replace with your excesses address
            deadline: now() + 10000,
            additionalData: SwapAdditionalData{
                minOut: 1, // swap will fail if a receiver should receive less than minOut of tokens as a result
                receiver,
            }
        };

        send(SendParameters{
            to: myJettonWalletAddress,
            value: GasSwapJettonToTon,
            body: JettonTransfer{
                queryId: 42,
                amount: offerAmount,
                destination: RouterAddress,
                responseDestination: myAddress(),
                forwardTonAmount: GasSwapJettonToTonFwd,
                forwardPayload: forwardPayload.toCell(),
            }.toCell()
        });
    }
}
```
